<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html>
<head>
<title>BDD Grammar</title>
</head>

<body>

<h2>EBNF</h2>

<p>Below is a detailed outlay of the BDD grammar JBehave understands.  It is expressed in <a href="http://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_Form">EBNF</a>.
<textarea
    class="xml:nogutter:nocontrols" name="code">
; JBehave Grammar expressed in EBNF (http://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_Form)

; The story or feature is a collection of scenarios
Story := Scenario+

; The scenario is identified by keyword "Scenario:" (or equivalent in I18n-ed locale),
; which is optional in the case of a single scenario.  
; It can optionally be followed by a description, which is expressed by any sequence of words
; that must not contain any keywords
; Then follow the Steps 
; Finally the Examples table, which if present will execute the scenario for as many table rows present
Scenario := "Scenario:"? Description? GivenScenarios? Step+ Examples?

; The scenario description
Description := (Word Space?)* 

; The word is any sequence of non-space characters that does not match a KeyWord
Word := NonSpaceCharacter* ; TODO express "!Keyword"

; The space between words
Space := " "

; Any non-space character
NonSpaceCharacter := [.] ; TODO express "!Space"

; The keywords which are reserved (or equivalent in I18n-ed locale)
KeyWord := "Scenario:" | "GivenScenarios:" | "Given" | "When" | "Then" | "And" 

; The comma-separated list of scenario resources that specify the scenarios to be run before the scenario steps
GivenScenarios:= "GivenScenarios:" (ScenarioResourcePath ','?)+

; The scenario resource path  
ScenarioResourcePath := PathCharacter+

; The characters allowed in a scenario resource path
PathCharacter := "/" | "." | "_" | Letter | Digit 

; The letter characters
Letter := [A-Z] | [a-z]

; The digit characters
Digit := [0-9]

; The scenario step is a step starting work followed by any number of characters
Step := StepStartingWord StepContent

; The step starting words (or equivalent in I18n-ed locale)
StepStartingWord :== ("Given" | "When" | "Then" | "And" ) 

; The step content is any sequence of characters that do not match a step starting word
StepContent := StepCharacter+ ; TODO express "!StepStartingWord"

; All characters are allowed in a scenario step content, including newlines
StepCharacter := [.]

; The examples table
Examples := "Examples:" Table

; The table comprises of a header row and data rows
Table := TableHeader TableRow+

; The table header contains the column names, separated by TableColumnSeparator
TableHeader := ((TableColumnSeparator TableCharacter+)+ TableColumnSeparator  Newline

; The table row contains the column values, separated by TableColumnSeparator
TableRow := ((TableColumnSeparator TableCharacter+)+ TableColumnSeparator  Newline

; The table column separator
TableColumnSeparator := "|"

; The table character can be any character, expect for TableColumnSeparator and Newline
TableCharacter := [.] ; TODO express !TableColumnSeparator and !Newline

; The new line character
Newline := "\n"
</textarea>
<div class="clear">
<hr />
</div>

</body>
</html>

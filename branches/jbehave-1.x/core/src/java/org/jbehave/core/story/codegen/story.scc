Package org.jbehave.core.story.codegen.sablecc;

Helpers
	letter = [['a'..'z'] + ['A'..'Z']];
	digit = ['0'..'9'];
	space = ' ';
	tab = 9;
	cr = 13;
	lf = 10;
	endl = cr | lf | cr lf;
	packagechar = letter | digit | '.' | '-';
	commentchar = '#';
	specialchar = '/' | '?' | '-' | '.' | '*' | '(' | ')';
	norwegianchar = 'æ'|'ø'|'å'|'Æ'|'Ø'|'Å';
	commentchars = space | tab | letter | digit | specialchar | norwegianchar;
	
Tokens
	title_keyword = 'Title:';
	package_keyword = 'Package:';
	scenario_keyword = 'Scenario:';
	as_a = 'As a';
	i_want = 'I want';
	so_that = 'So that';
	given = 'Given';
	when = 'When';
	then = 'Then';
	space = space;
	word = letter+;
	package_name = packagechar+;
	endl = endl;
	commentline = commentchar commentchars* endl;

Ignored Tokens
	commentline;
	
Productions
	story =
		title
		package?
		role?
		feature?
		benefit?
		scenario*;

	title = 
		title_keyword space phrase endl;
		
	package = 
		package_keyword space package_name endl;

	role =
		as_a space phrase endl;
		
	feature =
		i_want space phrase endl;
		
	benefit =
		so_that space phrase endl;
			
	scenario =
		scenario_title context* event? outcome*;
		
	scenario_title =
		scenario_keyword space phrase endl;
		
	context =
		given space phrase endl;
		
	event =
		when space phrase endl;

	outcome =
		then space phrase endl;

	phrase = 
		word_or_space+;
	
	word_or_space = 
		{word} word |
		{space} space;

		